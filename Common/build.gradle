plugins {
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
}

archivesBaseName = getArtifactID("common")

minecraft {
    version(minecraftVersion)
    accessWideners 'src/main/resources/hexalplat.accesswidener'
}

repositories {
    mavenCentral()

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so:
    // flatDir {
    //     dir 'libs'
    // }

    maven { url 'https://maven.blamejared.com' }

    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }

    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    compileOnly "${modID}:paucal-common-$minecraftVersion:$paucalVersion"
    compileOnly "${modID}:hexcasting-common-$minecraftVersion:$hexcastingVersion"
    compileOnly "vazkii.patchouli:Patchouli-xplat:$minecraftVersion-$patchouliVersion"
    compileOnly "software.bernie.geckolib:geckolib-fabric-1.19:$geckolibVersion"

    testImplementation "${modID}:paucal-common-$minecraftVersion:$paucalVersion"
    testImplementation "${modID}:hexcasting-common-$minecraftVersion:$hexcastingVersion"
    testImplementation "vazkii.patchouli:Patchouli-xplat:$minecraftVersion-$patchouliVersion"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

processResources {
    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {
        expand buildProps
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

setupJar(this)
